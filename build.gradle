buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

file "build.properties" withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

version = "1.12.2-1.0"
group = "net.montoyo.wd" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "webdisplays"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.2.2611"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20171003"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    maven {
        // computercraft
        url "http://maven.thiakil.com"
    }

    maven {
        // opencomputers
        url = "http://maven.cil.li/"
    }
}

dependencies {
    // this will use the API jars for CC and OC mods.  you must place the regular .jar
    // files in the run/mods/folder like normal for testing. also need to add luaj-jse-2.0.3jar to mods folder
    compileOnly "dan200.computercraft:ComputerCraft:${config.computercraft.version}:api"
    compileOnly "li.cil.oc:OpenComputers:MC1.12.2-${config.opencomputers.version}:api"
}

jar {
    exclude 'org/**'
    exclude 'net/montoyo/mcef/**'
    exclude 'assets/mcef/**'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
